import Mathlib.Analysis.SpecialFunctions.Integrals.Basic
import Mathlib.RingTheory.Real.Irrational
import Mathlib.Analysis.Convolution
import Mathlib.MeasureTheory.Integral.Prod
import Mathlib.MeasureTheory.Function.JacobianOneDim

open BigOperators Function Set Real Topology Filter
open ENNReal MeasureTheory Interval intervalIntegral
set_option linter.unusedVariables false
noncomputable section







/- # Last time

Last time we discussed differential calculus:
* `âˆ« x in a..b, f x` is the notation used for definite integrals
* `â„â‰¥0âˆž` is the type of extended nonnegative real numbers `[0, âˆž]`
* `MeasurableSpace X` is used to equip `X` with a Ïƒ-algebra
* `Î¼ : OuterMeasure X` resp. `Î¼ : Measure X` states that
  `Î¼` is an (outer) measure on `X`.
* `âˆ€áµ x âˆ‚Î¼, P x` means that `P` holds for almost all `x` (relative to `Î¼`)


**Today**
* finish integration
* inductive types
-/

section MeasureTheory

variable {X : Type*} [MeasurableSpace X] {Î¼ : Measure X}
variable {Y : Type*} [MeasurableSpace Y] {Î½ : Measure Y} {f : X â†’ Y}

example : âˆ€áµ x : â„, Irrational x := by
  sorry
  done



/- A map is (Borel-)measurable if preimages of measurable sets
under that map are measurable.
Note the similarity to the definition of continuity.
In particular, continuous functions are measurable. -/

example {f : X â†’ Y} : Measurable f â†”
    (âˆ€ s : Set Y, MeasurableSet s â†’ MeasurableSet (f â»Â¹' s)) := by rfl
#check Continuous.measurable

/- We can write `MeasurePreserving f Î¼ Î½` to state that `f : X â†’ Y`
maps the measure `Î¼ : Measure X` to `Î½ : Measure Y`,
i.e. `Î½ s = Î¼ (f â»Â¹' s)` for all (measurable) sets `s`.
This is important for Ergodic theory. -/
#check MeasurePreserving



/- A map `f` into a normed group is integrable when it is
measurable and the map `x â†¦ â€–f xâ€–` has a finite integral.
You can write `Integrable f Î¼` to say that `f` is integrable
w.r.t. `Î¼`, and `Integrable f` for `Î¼ = volume` -/
#check Integrable

example (f : â„ â†’ â„) (h1 : Continuous f) (h2 : HasCompactSupport f) :
    Integrable f :=
  Continuous.integrable_of_hasCompactSupport h1 h2



/- `Integrable` means that a function is integrable
on the whole domain.
You can use `IntegrableOn` to state that a function is
only integrable on some set. -/

#check IntegrableOn

example : Â¬ Integrable (fun _ â†¦ 1 : â„ â†’ â„) := by
  sorry
  done








/- We can take the integrals for functions intro a Banach space.
This version of the integral is called the *Bochner integral*.
The integral is denoted `âˆ« a, f x âˆ‚Î¼` -/
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [CompleteSpace E] {f : X â†’ E}

#check X â†’â‚[Î¼] E

example {f g : X â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    âˆ« x, f x + g x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ + âˆ« x, g x âˆ‚Î¼ :=
  integral_add hf hg


/-
* We can write `âˆ« x in s, f x âˆ‚Î¼` for the integral restricted to a set.
* We can abbreviate `âˆ« x, f x âˆ‚volume` to `âˆ« x, f x`
* We write `âˆ« x in a..b, f x âˆ‚Î¼` for the integral on an interval.
-/
example {s : Set X} (c : E) :
    âˆ« x in s, c âˆ‚Î¼ = Î¼.real s â€¢ c :=
  setIntegral_const c

example {f : â„ â†’ E} {a b c : â„} :
    âˆ« x in a..b, c â€¢ f x = c â€¢ âˆ« x in a..b, f x :=
  intervalIntegral.integral_smul c f

example {f : â„ â†’ E} {a b : â„} (h : a â‰¤ b) :
    âˆ« x in a..b, f x = âˆ« x in Ioc a b, f x :=
  integral_of_le h

example {f : â„ â†’ E} {a b : â„} (h : b â‰¤ a) :
    âˆ« x in a..b, f x = -âˆ« x in Ioc b a, f x :=
  integral_of_ge h


/- See the *Technical* section from last time if you want to see
more details on how the Bochner integral is defined. -/



/- If you have taken analysis III,
you will have seen some important theorems in measure theory.
General versions of these are also in Mathlib. -/

/- Here is a version of the dominated convergence theorem. -/
example {F : â„• â†’ X â†’ E} {f : X â†’ E} (bound : X â†’ â„)
    (hmeas : âˆ€ n, AEStronglyMeasurable (F n) Î¼)
    (hint : Integrable bound Î¼) (hbound : âˆ€ n, âˆ€áµ x âˆ‚Î¼, â€–F n xâ€– â‰¤ bound x)
    (hlim : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n : â„• â†¦ F n x) atTop (ð“ (f x))) :
    Tendsto (fun n â†¦ âˆ« x, F n x âˆ‚Î¼) atTop (ð“ (âˆ« x, f x âˆ‚Î¼)) :=
  tendsto_integral_of_dominated_convergence bound hmeas hint hbound hlim


/- Here is the statement of Fubini's theorem. -/
variable {X Y : Type*} [MeasurableSpace X] {Î¼ : Measure X} [SigmaFinite Î¼]
    [MeasurableSpace Y] {Î½ : Measure Y} [SigmaFinite Î½] in
example (f : X Ã— Y â†’ E) (hf : Integrable f (Î¼.prod Î½)) :
    âˆ« z, f z âˆ‚ Î¼.prod Î½ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ :=
  integral_prod f hf

/-
There are various versions of the change of variables theorem.
Here is one for functions in only 1 variable.
-/
example {s : Set â„} {f f' : â„ â†’ â„}
    (hs : MeasurableSet s)
    (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x)
    (hf : InjOn f s) (g : â„ â†’ E) :
    âˆ« x in f '' s, g x = âˆ« x in s, |f' x| â€¢ g (f x) :=
  integral_image_eq_integral_abs_deriv_smul hs hf' hf g

/-
Note that this has weaker assumptions versions you often see:
- `s` is not required to be open;
- `f` is not required to be continuously differentiable;
- because the integral of non-integrable functions has junk value 0,
  `g` is not required to be integrable.
-/

/- Here is a version of the change of variables formula for interval integrals. -/
#check integral_comp_smul_deriv''

end MeasureTheory








/-
## Inductive types

Lean's rules allow us to define *inductive types*.
An inductive type is a type where you (recursively) specify all elements.

For example, the following is a declaration of (a copy of) the natural numbers.
every element is either `zero` or `succ` of an (already constructed) natural number.

`zero` and `succ` are called the *constructors* of this inductive type.
-/
inductive NaturalNumber where
  | zero : NaturalNumber
  | succ (n : NaturalNumber) : NaturalNumber

#check NaturalNumber
#check NaturalNumber.zero
#check NaturalNumber.succ

open NaturalNumber
#check succ (succ (succ zero))



/-
Similarly, a list of elements from `Î±` is inductively defined by saying that:
- the empty list is a list
- if you have a list `l` and an element `x` in `Î±`,
  then adding `x` in front of `l` gives a new list.
-/

inductive MyList (Î± : Type*) where
  | nil : MyList Î±
  | cons : Î± â†’ MyList Î± â†’ MyList Î±



/- We'll use the `List` that Lean already knows,
since it comes with nice notation. -/
#check ([] : List â„•)
#check [5, 3]
#check 5 :: 3 :: []
#check List.cons 5 (List.cons 3 List.nil)




/- Inductive types allow you to define new operations on it
by *pattern matching*.

This defines a new function *recursively* by the given equations. -/



@[simp]
def appendLists {Î± : Type*} : List Î± â†’ List Î± â†’ List Î±
  | [],      bs => bs
  | a :: as, bs => a :: appendLists as bs

infix:60 " +' " => appendLists

example {Î± : Type*} (as : List Î±) : [] +' as = as := rfl

example {Î± : Type*} (a : Î±) (as bs : List Î±) :
    (a :: as) +' bs = a :: (as +' bs) := by rfl

/-
The default recursion is *structural recursion*.

Structural recursion allows you to define e.g.
a function `f : List Î± â†’ Î²` by defining
- `f [] := f_nil`
- `f (a :: as) := f_cons a as (f as)`
for some expressions `f_nil` and `f_cons`.
We write `f_cons a as (f as)` to signify that
this expression can depend on `a`, `as`` and `f as`
(but not `f` applied to other values.
-/


/- We can prove properties about inductive types using `induction`. -/

example {Î± : Type*} (as bs cs : List Î±) :
    (as +' bs) +' cs = as +' (bs +' cs) := by
  sorry
  done

example {Î± : Type*} (as : List Î±) :
    as +' [] = as := by
  sorry
  done


/- As another example, here are *two* ways to define addition
on the natural numbers I defined above. -/

@[simp]
def myAdd : NaturalNumber â†’ NaturalNumber â†’ NaturalNumber
  | zero,   m => m
  | succ n, m => succ (myAdd n m)


@[simp]
def myAdd' : NaturalNumber â†’ NaturalNumber â†’ NaturalNumber
  | n, zero  => n
  | n, succ m => succ (myAdd' n m)


/- They are not trivially equal! -/

example : myAdd = myAdd' := by
  sorry
  done

/- Under the hood, both (structural) recursion and induction
come from a *recursor*, that is automatically generated when
you write an `inductive` command. -/

#check Nat.rec

/-
Given
- `P : â„• â†’ Sort u` (think: `P : â„• â†’ Prop`)
- `c_0 : P 0`
- `c_succ : âˆ€ n, P n â†’ P (n + 1)`

then the function `g = @Nat.rec P c_0 c_succ : âˆ€ n : â„•, P n`
is the function defined by
- `g 0 := c_0`
- `g (n + 1) := c_succ n (g n)`

These equalities hold *by definition*.
In other words, `rfl` can prove these two equalities:
- `@Nat.rec P c_0 c_succ 0 = c_0`
- `@Nat.rec P c_0 c_succ (n + 1) = c_succ n (@Nat.rec P c_0 c_succ n)`
These are called the *computation rules* for `â„•`.

Let's see some examples. -/


def myFac : â„• â†’ â„• :=
  sorry

lemma myFac_succ (n : â„•) : myFac (n + 1) = (n + 1) * myFac n := by
  sorry

lemma myFac_pos (n : â„•) : 0 < myFac n := by
  sorry
  done

/-
In summary: defining a inductive type like
```
inductive NaturalNumber where
  | zero : NaturalNumber
  | succ (n : NaturalNumber) : NaturalNumber
```
gives 4 things:
* A new type `NaturalNumber : Type`
* *constructors* `NaturalNumber.zero` and `NaturalNumber.succ`
* A *recursor* `NaturalNumber.rec` that stated how to define functions
  recursively and prove things inductively.
* *computation rules* that state how to compute
  when the recursor is applied to a constructor.
-/




/-
### Well-founded recursion

Lean also support *non-structural* recursion.
This is implemented using *well-founded recursion*.
Lean tries to prove automatically that the arguments
in the recursive calls are smaller than the input argument.
-/

/- the Fibonacci sequence -/
def F : â„• â†’ â„•
  | 0 => 0
  | 1 => 1
  | n + 2 => F (n + 1) + F n

/- the number of factors 2 in a number -/
def twoVal : â„• â†’ â„•âˆž
  | 0 => âŠ¤
  | n + 1 =>
    if (n + 1) % 2 = 0 then
      twoVal ((n + 1) / 2) + 1
    else
      0

/- The Ackermann function -/
def A : â„• â†’ â„• â†’ â„•
| 0,     n     => n + 1
| m + 1, 0     => A m 1
| m + 1, n + 1 => A m (A (m + 1) n)


/- `(b, n) â†¦ âŒŠlog_b(n)âŒ‹`, i.e. the logarithm rounded down.
In this case, we need to help Lean -/
def myLog (b : â„•) : â„• â†’ â„•
  | n => if h : b â‰¤ n âˆ§ 1 < b then myLog b (n / b) + 1 else 0
decreasing_by
  exact Nat.div_lt_self (by cutsat) h.2




/-
*Well-founded recursion* uses a special inductive type
`Acc` (for *accessibility*).

Take a look at these definitions if you want to know the details.
-/
#check WellFounded
#check Acc



/-
### Other inductive types

Many types in Lean are inductive types.
Here are some examples. Each of these inductive types
-/

/- Cartesian product of types. -/
inductive CartesianProduct (A B : Type*) where
  | pair : A â†’ B â†’ CartesianProduct A B

/- Coproduct (disjoint union) of types. -/
inductive DisjointUnion (A B : Type*) where
  | left  : A â†’ DisjointUnion A B
  | right : B â†’ DisjointUnion A B

/- specific enumerated types. -/
inductive Booleans where
  | true : Booleans
  | false : Booleans

inductive SingletonType where
  | singlePoint : SingletonType

inductive EmptyType where
-- 0 constructors!



/- We can also define *inductive propositions*. -/

inductive Conjunction (A B : Prop) : Prop where
  | and_intro : A â†’ B â†’ Conjunction A B

inductive Disjunction (A B : Prop) : Prop where
  | left  : A â†’ Disjunction A B
  | right : B â†’ Disjunction A B

inductive Truth : Prop where
  | trivial : Truth

inductive Falsity : Prop where

inductive Existential (A : Type*) (P : A â†’ Prop) : Prop where
  | intro : âˆ€ x, P x â†’ Existential A P


/- Even equality is defined inductively.
This is a special: it is a inductive *family* of propositions.
We define for each type `A` we define `Equality A a b`
simultaneously for all `a b : A`. -/
inductive Equality (A : Type*) : A â†’ A â†’ Prop where
  | reflexivity : âˆ€ a : A, Equality A a a

/- We can define the proposition stating that a type is non-empty. -/
inductive IsNonempty (A : Type*) : Prop where
  | intro : A â†’ IsNonempty A


/- Another example if inductive families comes from constructions
that construct the smallest collection of objects satisfying some property.

For example, if `g` is any collection of subsets of a type `X`,
the following gives the smallest topology where all sets in `g` are open.
Equivalently: the intersection of all topologies containing `g`. -/
inductive GenerateOpen {X : Type*} (g : Set (Set X)) : Set X â†’ Prop
  | basic : âˆ€ s âˆˆ g, GenerateOpen g s
  | univ : GenerateOpen g univ
  | inter : âˆ€ s t, GenerateOpen g s â†’ GenerateOpen g t â†’ GenerateOpen g (s âˆ© t)
  | sUnion : âˆ€ S : Set (Set X), (âˆ€ s âˆˆ S, GenerateOpen g s) â†’ GenerateOpen g (â‹ƒâ‚€ S)

def generateFrom {X : Type*} (g : Set (Set X)) : TopologicalSpace X where
  IsOpen := GenerateOpen g
  isOpen_univ := GenerateOpen.univ
  isOpen_inter := GenerateOpen.inter
  isOpen_sUnion := GenerateOpen.sUnion

/-
Something special happens with inductive propositions.
Some propositions (`âˆ§`, `True`, `False`, `=`) can eliminate
to *any* sort (all types and `Prop`)
Other propositions (`âˆƒ`, `âˆ¨`, `Nonempty`) can only eliminate to `Prop`.

Look at type type of `motive` in the recursors below.

The difference is that the propositions `âˆƒ`, `âˆ¨` and `Nonempty` can be proven
with either different constructors or one constructor applied to different arguments.
If such a proposition could eliminate to any type, then (together with the computation rule)
you could derive a contradiction.
-/
#check Conjunction.rec
#check Disjunction.rec
#check Nonempty.rec
#check Equality.rec
