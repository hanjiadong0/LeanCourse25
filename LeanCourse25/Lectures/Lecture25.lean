import LeanCourse25.Lectures.DifferentialGeometryNotation
import LeanCourse25.Lectures.SmoothEmbedding
import Mathlib.Geometry.Manifold.LocalDiffeomorph
import Mathlib.Geometry.Manifold.Instances.Real
import Mathlib.Geometry.Manifold.Instances.Sphere

import Mathlib.Geometry.Manifold.VectorBundle.SmoothSection
import Mathlib.Geometry.Manifold.VectorBundle.Hom
import Mathlib.Geometry.Manifold.VectorBundle.Pullback
import Mathlib.Geometry.Manifold.VectorField.LieBracket
import Mathlib.Geometry.Manifold.VectorBundle.Tangent
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv
import Mathlib.Geometry.Manifold.Riemannian.Basic

open ContDiff Manifold Function Metric Module Set TopologicalSpace
noncomputable section

/-
# Announcements

* Assignment 13 has many practice exercises, but does not require you to hand in anything.
  Work on your project instead.

* There will be a draft of the mock exam by the end of the week.

* There will be an "advanced Lean seminar" next semester
  (officially called Graduate Seminar in Applied Logic, S4A6).
  It will cover more advanced aspects, such as considerations how to design a large library,
  exploring the design of a particular area, learning how to review code or
  more advanced areas of Lean (e.g. the typeclass system, designing the simp set, ...).
  No knowledge beyond this class is necessary.
  Wishes for topics are welcome and may still be considered.
  The **initial meeting** will be on Thursday, February 12 at 10.15.

-/

/- # Last time: smooth manifolds

* topological and smooth manifolds, possibly with boundary and corners
* examples: normed spaces, open/closed intervals, spheres
* formalising them: charted spaces and models with corners
* interior and boundary points
* (continuous) differentiability of maps between manifolds

-/

/- Addendum: interior and boundary points -/

-- Let `M` and `N` be `C^n` manifolds.
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] {n : WithTop â„•âˆ}
  [IsManifold I n M] {x : M}
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {G : Type*} [TopologicalSpace G] (J : ModelWithCorners ğ•œ F G)
  {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  [IsManifold J n N]

example : extChartAt I x = I âˆ˜ chartAt H x := by rfl

-- `x` is an interior point of `M` iff it lies in the interior of the range of its extended chart.
example : I.IsInteriorPoint x â†” extChartAt I x x âˆˆ interior (range I) := by rfl

section

variable (n m : â„•) (E' E'' : Type*) [NormedAddCommGroup E'] [InnerProductSpace â„ E']
  [NormedAddCommGroup E''] [InnerProductSpace â„ E'']

-- The product of two spheres ğ•Š^n Ã— ğ•Š^m is a manifold.
variable [Fact (finrank â„ E' = n + 1)] [Fact (finrank â„ E'' = m + 1)] in
#synth IsManifold ((ğ“¡ n).prod (ğ“¡ m)) Ï‰ ((sphere (0 : E') 1) Ã— (sphere (0 : E'') 1))

-- The 2-torus ğ•‹Â² = ğ•ŠÂ¹ Ã— ğ•ŠÂ¹ is a two-dimensional analytic manifold.
variable [Fact (finrank â„ E' = 2)] in
#synth IsManifold ((ğ“¡ 1).prod (ğ“¡ 1)) Ï‰ ((sphere (0 : E') 1) Ã— (sphere (0 : E') 1))

-- The disjoint union of ğ•ŠÂ² with itself is also a two-dimensional analytic manifold.
variable [Fact (finrank â„ E' = 3)] in
#synth IsManifold ((ğ“¡ 2)) Ï‰ ((sphere (0 : E') 1) âŠ• (sphere (0 : E') 1))

end


/- # Differential geometry (continued)

## Tangent space & tangent bundle -/

-- A smooth manifold has a tangent space at each point.
-- Let's review the pen-and-paper definition first.


#check TangentSpace I x
#check TangentSpace% x



/-
In Lean, if `M` is a manifold modeled on `(H, E)`, the tangent space at a point `p`
is in fact just `E`. Relying heavily on this fact is *defeq abuse*: it's using a
definitional equality to an overly large extent, and creates brittle code. -/
example (p : M) : TangentSpace I p = E := rfl

/- We could the following, for example.
Don't do this at home; if you feel you need to, usually something has gone wrong somewhere else. -/

instance (p : M) : NormedAddCommGroup (TangentSpace I p) := inferInstanceAs (NormedAddCommGroup E)
example (p : M) : NormedSpace ğ•œ (TangentSpace I p) := inferInstanceAs (NormedSpace ğ•œ E)



















-- All tangent spaces fit together to give the tangent bundle of `M`.
#check TangentBundle I M


/- A differentiable map between manifolds induces a map on tangent spaces. -/

example (f : M â†’ N) (_hf : MDifferentiable I J f) (x : M) :
    TangentSpace I x â†’L[ğ•œ] TangentSpace J (f x) :=
  mfderiv I J f x
-- Or, in cleaner notation
example (f : M â†’ N) (_hf : MDiff f) (x : M) : TangentSpace% x â†’L[ğ•œ] TangentSpace% (f x) :=
  mfderiv% f x

/- It also induces a map on the tangent bundle. -/

example (f : M â†’ N) (_hf : MDifferentiable I J f) :
    TangentBundle I M â†’ TangentBundle J N :=
  tangentMap I J f

example [IsManifold I 1 M] [IsManifold J 1 N] (f : M â†’ N) (hf : ContMDiff I J âŠ¤ f) :
    ContMDiff I.tangent J.tangent âŠ¤ (tangentMap I J f) :=
  hf.contMDiff_tangentMap le_rfl

example [IsManifold I 1 M] [IsManifold J 1 N] (f : M â†’ N) (hf : CMDiff âŠ¤ f) :
    CMDiff âŠ¤ (tangentMap% f) :=
  hf.contMDiff_tangentMap le_rfl

example [AddGroup N] [LieAddGroup J âŠ¤ N] {f g : M â†’ N} {n : â„•âˆ}
    (hf : ContMDiff I J n f) (hg : ContMDiff I J n g) :
    ContMDiff I J n (f + g) :=
  hf.add hg

-- Let `V` be a vector field on `M`: two completely equivalent phrasings.
variable {V W : (x : M) â†’ TangentSpace I x} {V : (x : M) â†’ TangentSpace% x} [IsManifold I 1 M]
-- Suppose `V` is smooth. The notation `T%` converts `V` from a dependent function
-- to a function into the tangent bundle. We will explain this in more detail later.
  (hV : CMDiff âŠ¤ (T% V))

-- What happens if we comment the `IsManifold I 1 M`? Let's try to understand the error message.

-- The sum of two smooth vector fields is smooth.
-- We're using a more general lemma about smooth sections in a *smooth vector bundles*:
-- we will define these terms later today.
example {V W : (x : M) â†’ TangentSpace% x} (hV : CMDiff âŠ¤ (T% V)) (hW : CMDiff âŠ¤ (T% W)) :
    CMDiff âŠ¤ (T% (V + W)) := by
  exact ContMDiff.add_section hV hW


-- One interesting operation on vector fields is the Lie bracket:
-- let us review the definition on paper first.

-- This is the Lean definition.
#check VectorField.mlieBracket

-- There is also a version within a set.
#check VectorField.mlieBracketWithin

open VectorField
-- The lie bracket is anti-symmetric and alternating.
example {s : Set M} : mlieBracketWithin I V W s = - mlieBracketWithin I W V s := by
  exact mlieBracketWithin_swap

example : mlieBracket I V V = 0 := mlieBracket_self

-- It also satisfies the Jacobi identity
#check VectorField.leibniz_identity_mlieBracket

-- The following result was formalised quite recently.
/-- **Product rule for Lie brackets**: given two vector fields `V` and `W` on `M` and a function
`f : M â†’ ğ•œ`, we have `[V, f â€¢ W] = (df V) â€¢ W + f â€¢ [V, W]`. -/
lemma mlieBracket_smul_right {f : M â†’ ğ•œ} (hf : MDiffAt f x) (hW : MDiffAt (T% W) x) :
    mlieBracket I V (f â€¢ W) x = (mfderiv% f x) (V x) â€¢ (W x) + (f x) â€¢ mlieBracket I V W x := by
  sorry

-- Fact: given two vector fields `X` and `Y`, their *local flows* commute iff `[X, Y] = 0`.






/- ## Smooth vector bundles -/

/- Given a continuous surjection `Ï€ : Z â†’ M`.
A trivialization of `Ï€` at `x : M` with fiber `F`
is a neighborhood `U` of `x` and a homeomorphism
`Ï• : Ï€ â»Â¹' U â†’ U Ã— F` that commutes with projections. -/
#check Trivialization

/- Fiber bundles have trivializations around each point in the base manifold -/
#check FiberBundle

/- In vector bundles the trivializations induce linear maps on the fibers.
Interestingly, for infinite-dimensional manifolds
you need an additional continuity condition. -/
#check VectorBundle

/- In smooth vector bundles the trivializations are smooth. -/
#check ContMDiffVectorBundle


-- If `M` is a `C^{n+1}`-manifold, the tangent bundle `TM` is a `C^n` vector bundle.
#check TangentBundle.contMDiffVectorBundle


open Bundle
/- Let `Eâ‚` and `Eâ‚‚` be smooth vector bundles over `M`. -/

variable
  (Fâ‚ : Type*) [NormedAddCommGroup Fâ‚] [NormedSpace ğ•œ Fâ‚]
  (Eâ‚ : M â†’ Type*) [âˆ€ x, AddCommGroup (Eâ‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚ x)]
  [âˆ€ x : M, TopologicalSpace (Eâ‚ x)] [TopologicalSpace (TotalSpace Fâ‚ Eâ‚)]
  [FiberBundle Fâ‚ Eâ‚] [VectorBundle ğ•œ Fâ‚ Eâ‚]
  [ContMDiffVectorBundle n Fâ‚ Eâ‚ I]
variable
  (Fâ‚‚ : Type*) [NormedAddCommGroup Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚]
  (Eâ‚‚ : M â†’ Type*) [âˆ€ x, AddCommGroup (Eâ‚‚ x)] [âˆ€ x, Module ğ•œ (Eâ‚‚ x)]
  [âˆ€ x : M, TopologicalSpace (Eâ‚‚ x)] [TopologicalSpace (TotalSpace Fâ‚‚ Eâ‚‚)]
  [FiberBundle Fâ‚‚ Eâ‚‚] [VectorBundle ğ•œ Fâ‚‚ Eâ‚‚]
  [ContMDiffVectorBundle n Fâ‚‚ Eâ‚‚ I]


/- A map `s : M â†’ Eâ‚` is called a *section* of a fibre bundle `Ï€ : Eâ‚ â†’ M` if `Ï€ âˆ˜ s = id`,
i.e. `s x âˆˆ Eâ‚ x` for all `x : M`. In Lean, this is simply a dependent function.
-/
variable {s : (x : M) â†’ Eâ‚ x}

-- Observe that a vector field on `M` is precisely a section of its tangent bundle `TM`.









/- If `E` is a smooth vector bundle, "`s` is a smooth section" is a sensible statement.
`ContMDiff` requires a non-dependent function as an argument.
The expression `T% s` takes the section `s` and converts it to a non-dependent section,
i.e. a map into the bundle's total space. (In other words, it denotes post-composition with
the inclusion of each bundle fiber into the total space.)
-/
variable {hs : CMDiff n (T% s)}

-- This is the equivalent expression without using `CMDiff` and `T%`.
variable {hs : ContMDiff I (I.prod ğ“˜(ğ•œ, Fâ‚)) n (fun x â†¦ TotalSpace.mk' Fâ‚ x (s x))}

-- There is also a type of bundled smooth sections, with special notation.
-- Note: "bundled" has nothing to do with vector or fiber bundles;
-- it refers to the fact that these combine a section with a proof of smoothenss.
#check ContMDiffSection
variable {t : Câ‚›^nâŸ®I; Fâ‚, Eâ‚âŸ¯}

-- The sum of smooth sections is a smooth section.
-- This was the statement we encountered about vector fields above.
example {s t : (x : M) â†’ Eâ‚ x} {hs : CMDiff n (T% s)} {ht : CMDiff n (T% t)} :
    CMDiff n (T% (s + t)) :=
  hs.add_section ht

-- We can also add bundled smooth sections:
-- under the hood, this is using the above example.
example {s' t' : Câ‚›^nâŸ®I; Fâ‚, Eâ‚âŸ¯} : Câ‚›^nâŸ®I; Fâ‚, Eâ‚âŸ¯ :=
  s' + t'
example {s' t' : Câ‚›^nâŸ®I; Fâ‚, Eâ‚âŸ¯} : (s' + t') x = s' x + t' x := rfl

/- The product of two bundles is a smooth vector bundle. -/

#synth ContMDiffVectorBundle n (Fâ‚ Ã— Fâ‚‚) (Eâ‚ Ã—áµ‡ Eâ‚‚) I


/- We can take construct the bundle of continuous linear maps between bundles. -/

variable [âˆ€ x, IsTopologicalAddGroup (Eâ‚ x)] [âˆ€ x, IsTopologicalAddGroup (Eâ‚‚ x)]
  [âˆ€ x, ContinuousSMul ğ•œ (Eâ‚‚ x)]

#synth ContMDiffVectorBundle n (Fâ‚ â†’L[ğ•œ] Fâ‚‚) (fun (b : M) â†¦ Eâ‚ b â†’L[ğ•œ] Eâ‚‚ b) I

/- We can pull back vector bundles. -/

variable (f : C^nâŸ®J, N; I, MâŸ¯)

#synth ContMDiffVectorBundle n Fâ‚ ((f : N â†’ M) *áµ– Eâ‚) J






section

-- Let us explore some special kinds of maps.

#check Diffeomorph

-- If `f` is a diffeomorphism, its differential is invertible.
-- (This follows easily from the chain rule.)
#check Diffeomorph.mfderivToContinuousLinearEquiv

-- Mathlib also knows about local diffeomorphisms: `f` is a local diffeomorphism
-- if for every point `p`, there exist open subsets `U` and `V` of `p` and `f p`
-- and a diffeomorphism `Î¦ : U â‰ƒ V` which agrees with `f` on `U`.
#check IsLocalDiffeomorph

#check Diffeomorph.isLocalDiffeomorph

-- If `M` is finite-dimensional, `f` is an immersion if each differential
-- `mfderiv% f p` is injective. Equivalently, each `p : M` has suitable
-- charts in which `f` looks like a map `u â†¦ (u, 0)`.
-- In infinite dimensions, these definitions are no longer equivalent,
-- the second one is the correct condition (and implies the first one).
#check IsImmersion

-- mathlib also knows about smooth embeddings: smooth embeddings are smooth immersions automatically
#check IsSmoothEmbedding

example {f : M â†’ N} (hf : IsSmoothEmbedding I J n f) : IsImmersion I J n f := by
  exact hf.isImmersion


/- Patrick Massot, Oliver Nash and Floris van Doorn have formalized
a result in differential geometry called *Gromov's h-principle*.

In particular, this allows you to abstractly define an eversion of a sphere. -/

variable (E : Type*) [NormedAddCommGroup E] [InnerProductSpace â„ E]
  [Fact (finrank â„ E = 3)]

local notation "â„Â³" => E
local notation "ğ•ŠÂ²" => sphere (0 : â„Â³) 1

theorem sphere_eversion : âˆƒ f : â„ â†’ ğ•ŠÂ² â†’ â„Â³,
    (ContMDiff (ğ“˜(â„, â„).prod (ğ“¡ 2)) ğ“˜(â„, â„Â³) âˆ (uncurry f)) âˆ§
    (f 0 = fun x : ğ•ŠÂ² â†¦ (x : â„Â³)) âˆ§
    (f 1 = fun x : ğ•ŠÂ² â†¦ -(x : â„Â³)) âˆ§
    âˆ€ t, IsImmersion (ğ“¡ 2) ğ“˜(â„, â„Â³) âŠ¤ (f t) :=
  sorry -- not yet in Mathlib

end

-- Riemannian metrics
#check IsRiemannianManifold


/- Coming soon:
* connections (covariant derivatives)
* the Levi-Civita connection
* curvature
* geodesics
* the exponential map

-/
